# +-------------------------------+--------------------------+
# | IRCBot.ldpl by Martín del Río | IRC bot library for LDPL |
# +-------------------------------+--------------------------+
# | C++ code based on the code of SirLogsalot by gkbrk:      |
# | https://github.com/gkbrk/SirLogsalot                     |
# +----------------------------------------------------------+

EXTENSION "ldpl_irc_bot.cpp"

DATA:
    # LDPLIRCBot Library Variables ------------------------------------------------------------------------
    ircbot/nick is external text        # The nick of our bot
    ircbot/server is external text      # The IRC server we want to connect to
    ircbot/port is external text        # The port of the IRC server we want to connect to
    ircbot/channels is external text    # The channels we want to join (comma separated)
    ircbot/to is external text          # The recipient of our message
    ircbot/message is external text     # The mensaje we received or want to send
    ircbot/from is external text        # The user who has sent the last message or joined / left a channel
    ircbot/channel is external text     # The channel where the action happened
    # -----------------------------------------------------------------------------------------------------

PROCEDURE:
    # Bot set-up
    store "myLDPLBot" in ircbot/nick                    # Choose a nick for our bot (must be unique)
    store "185.30.166.37" in ircbot/server              # The IP of the server our bot will connect to
    store "6667" in ircbot/port                         # The port of the server our bot will connect to
    store "#somechannel,#someotherchannel" in ircbot/channels   # The channels our bot will join
    call external ircbot/create-bot                     # Create the bot and connect to the server

    # (Execution never reaches this point, for ircbot/create-bot is a blocking function)

    # +------------------------------------------------------------------------------------+
    # | These three next sub-procedures: ircbot/on-message, ircbot/on-join, ircbot/on-part |
    # | must be declared even if you don't want to use them or the bot won't compile.      |
    # | (In you don't need them just leave them empty.)                                    |
    # +------------------------------------------------------------------------------------+

    # +-------------------+-------------------------------+
    # | ircbot/on-message | called when: message received |
    # +-------------------+-------------------------------+
    # This sub-procedure is executed when our bot receives a message (both private or
    # from a channel). When this function is called, variables are setup like this:
    # -> ircbot/message contains the message that was received.
    # -> ircbot/from contains the nick of the user who sent the message.
    # -> ircbot/channel contains the channel the message was sent to (or the value of
    # ircbot/nick if the message was sent privately to our bot).
    external sub-procedure ircbot/on-message
        # Log to console the message we received
        display ircbot/from " has sent " ircbot/message " to the channel " ircbot/channel crlf

        # Echo the message to the channel
        store ircbot/channel in ircbot/to
        call external ircbot/send-message
    end sub-procedure

    # +----------------+----------------------------------+
    # | ircbot/on-join | called when: user joined channel |
    # +----------------+----------------------------------+
    # This sub-procedure is executed when someone joins a channel our bot is in.
    # When this function is called, variables are setup like this:
    # -> ircbot/from contains the nick of the user who joined the channel.
    # -> ircbot/channel contains the channel the user joined to.
    external sub-procedure ircbot/on-join
        # Log to console that the user has joined the channel.
        display ircbot/from " has joined the channel " ircbot/channel crlf
    end sub-procedure

    # +----------------+--------------------------------+
    # | ircbot/on-part | called when: user left channel |
    # +----------------+--------------------------------+
    # This sub-procedure is executed when someone leaves a channel our bot is in.
    # When this function is called, variables are setup like this:
    # -> ircbot/from contains the nick of the user who parted from the channel.
    # -> ircbot/channel contains the channel the user left.
    external sub-procedure ircbot/on-part
        # Log to console that the user has joined the channel.
        display ircbot/from " has left the channel " ircbot/channel crlf
    end sub-procedure

    # +---------------------+-----------------------+
    # | ircbot/send-message | used to send messages |
    # +---------------------+-----------------------+
    # This library also includes the ircbot/send-message sub-procedure, that's used
    # to send messages to other users or channels. When called, ircbot/send-message
    # sends the message stored in ircbot/message to the user or channel stored in
    # the variable ircbot/to. 